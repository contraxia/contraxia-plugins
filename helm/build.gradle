apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
	systemTest
}

jar {
	// bundle common project with plugin
	from { project(':common').file('build/classes/java/main') }
}

dependencies {
	compile gradleApi()
	compile localGroovy()

	testCompile project(':common')
	compileOnly project(':common')

	compile 'org.apache.httpcomponents:httpclient:4.5.2'
	compile 'de.undercouch:gradle-download-task:3.3.0'

	testCompile project(':oc')
	systemTestCompile project(':oc')
}

systemTest.outputs.upToDateWhen { false }

pluginBundle {
	website = 'https://github.com/rmee/gradle-plugins/tree/master/helm'
	vcsUrl = 'https://github.com/rmee/gradle-plugins/tree/master/helm'
	description = 'Access to Helm command line tool from Gradle to perform Kubernetes package management.'
	tags = ['helm', 'kubernetes', 'package']

	plugins {
		'helm' {
			id = 'com.github.rmee.helm'
			displayName = 'helm'
		}
	}
}

systemTest {
	testLogging.showStandardStreams = true
}

pluginUnderTestMetadata {
	pluginClasspath = files(pluginClasspath, project(':common').file('build/classes/java/main'),
			project(':kubectl').file('build/classes/java/main'), project(':kubectl').file('build/resources/java/main'),
			project(':oc').file('build/classes/java/main'), project(':oc').file('build/resources/java/main'))
}